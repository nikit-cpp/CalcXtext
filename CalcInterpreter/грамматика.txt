http://ru.wikipedia.org/wiki/Расширенная_форма_Бэкуса_—_Наура
Терминалы записаны большими буквами, нетерминалы - маленькими.
Вместо некоторых терминалов (MUL, DIV, LP, ...) для наглядности
записаны соответствующие им лексемы, заключённые в '':
'*', '/', '(', ...
Начиная с символов '//' - и до конца строки - комметарий

program = 
	{ instr } EXIT

instr = 
		';'
	|	if_
	|	void_func ';'
	|	expr ';'

if_ =
	IF '(' expr ')' block [ ELSE block ]
	
block =
	'{'	{ instr } '}'

void_func =
		print
	|	add
	|	del
	|	reset
	|	set
	|	help
	|	state
	
print =
	PRINT [ expr ]

add =
	USER_DEFINED_NAME [ '=' expr ]
	
del =
	DEL ( '*' | USER_DEFINED_NAME )
	
set =
	SET setname '=' currTok
	
reset =
	RESET ( '*' | TABLE | setname )
		
setname =
	ARGS_AUTO_END | AUTO_END | PRINT_TOKENS |
	PRECISION | ERRORS | STRICTED | AUTO_PRINT | GREEDY_FUNC
	
help =
	HELP
	
state =
	STATE
	
expr =
	term { ( '+' | '(+)' | '-' ) term }
	
term =
	degree { ( '*' | '/' ) degree }

degree =
	factorial [ '^' degree ]

factorial =
	prim { '!' }

prim =
		NUMBER
	|	USER_DEFINED_NAME [ '=' expr ]
	|	'-' prim
	|	'(' expr ')'
	|	func

func =
	of_radian '(' expr ')'

of_radian =
		SIN
	|	COS
